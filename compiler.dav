// File: compiler.dav
// Author: David T.
// Description: Stage 1 compiler for the dav language.
// This file is compiled by the Stage 0 Python compiler.


// =============================================================
// Global Storage
// =============================================================

// --- Tokenizer Storage ---
beg char* token_types[1000];
beg int token_values[1000]; // Stores index into token_pool, or -1
beg int token_lines[1000];
beg int token_cols[1000];
beg char token_pool[50000]; // String pool for token values
beg int n_tokens = 0;      // Total number of tokens found


// --- Parser State ---
beg int parser_pos = 0; // Current token index for the parser
beg char* expr_type;    // Type of the last parsed expression, works like a forgetful stack


// =============================================================
// Function Declarations
// =============================================================

// --- Lexer Helpers ---
ah int is_letter(char c);
ah int is_digit(char c);
ah int is_space(char c);
ah int is_ident_char(char c);
ah char* check_keywords(char* s);
ah int add_simple_token(int index, char* type, int line, int col);

ah int tokenize(char* source_code);

// --- Parser Helpers ---
ah char* parse();
ah char* global_decl();
ah char* fn_decl();
ah char* statement();
ah char* let_stmt(int is_global);
ah char* print_stmt();
ah char* if_stmt();
ah char* while_stmt();
ah char* return_stmt();
ah char* id_stmt();
ah char* comment_stmt();

ah char* expr();
ah char* logical();
ah char* relational();
ah char* additive();
ah char* multiplicative();
ah char* unary();
ah char* atom();

ah char* peek();
ah int next();
ah int expect(char* kind);

ah int clear_local_symbols();
ah char* get_symbol_type(int is_global, char* name);
ah int add_symbol(int is_global, char* name, char* type);

ah int str_ends_with(char* s, char c);
ah char* op_to_c_op(char* tok_type);

// --- Symbol Table Storage ---
// We store 'char*' pointers for names and types.
// The Stage 0 compiler will get the name strings from the token_pool.
// The type strings will be string literals (e.g., "int", "char*").

// Global Scope (self.env)
beg char* global_names[100];
beg char* global_types[100];
beg int n_globals = 0;

// Local Scope (self.variables)
beg char* local_names[100];
beg char* local_types[100];
beg int n_locals = 0;


ah void print_token() {
    boo("--- Stored Tokens ---");
    beg int i = 0;
    beg int pool_pos = 0;
    
    while (i < n_tokens) {
        boo(token_types[i]);
        
        pool_pos = token_values[i];
        if (pool_pos != -1) {
            // It has a value. Print it using pointer arithmetic.
            boo(token_pool + pool_pos);
        }
        i = i + 1;
    }
}


// =============================================================
// Main Entry Point
// =============================================================

ah int main() {
    // Test code with all global declaration types
    beg char* code = "// My Stage 1 Compiler\n\nbeg int global_var = 10;\n\nah int my_func();\n\nah int main() {\n    boo(global_var);\n}\n";
    
    // 1. Tokenize
    boo("--- Tokenizing ---");
    n_tokens = tokenize(code);
    boo("--- Tokenizing Complete ---");

    // 2. Parse
    boo("--- Parsing ---");
    beg char* c_code = parse();
    
    boo("--- Generated C Code ---");
    boo(c_code);
    
    return 0;
}


// =============================================================
// Parser
// =============================================================

ah char* parse() {
    // Main parser entry point.
    // Loops until EOF, parsing all global declarations.
    beg char* code_buffer = ""; // This will hold the entire generated C file

    while peek() != "EOF" {
        code_buffer = code_buffer + global_decl();
    }
    
    return code_buffer;
}

ah char* global_decl() {
    // Dispatches to the correct parser function
    // based on the next token.
    beg char* tok = peek();

    if tok == "FN" {
        return fn_decl();
    } else if tok == "LET" {
        return let_stmt(1);
    } else if tok == "COMMENT" {
        return comment_stmt();
    } else {
        // Error handling
        beg int tok_line = token_lines[parser_pos];
        boo("Error: Unexpected global token on line " + itos(tok_line));
        boo("Expected FN, LET, or COMMENT, but got: " + tok);
        
        // Consume the bad token to prevent infinite loop
        next(); 
        return ""; // Return empty string for this declaration
    }
}

ah char* fn_decl() {
    // Parses a function declaration or definition
    beg int fn_tok_idx = expect("FN");
    beg int indent = token_cols[fn_tok_idx];
    beg int line_num = token_lines[fn_tok_idx];
    
    // --- Get Type ---
    beg char* fn_type = "int"; // Default type
    if peek() == "TYPE" {
        beg int fn_type_idx = next();
        fn_type = token_pool + token_values[fn_type_idx];
    }
    
    // --- Get Name ---
    beg fn_name_idx = expect("ID");
    beg char* fn_name = token_pool + token_values[fn_name_idx];

    // --- Add to global ---
    add_symbol(1, fn_name, fn_type);

    // --- Parse parameters
    expect("LPAREN");

    // Parallel arrays to store parameters
    beg char* param_types[20];
    beg char* param_names[20];
    beg char* param_arrays[20];
    beg int n_params = 0;

    while peek() != "RPAREN" {
        // Get param type (default int)
        beg char* param_type = "int";
        if peek() == "TYPE" {
            beg int param_type_idx = next();
            param_type = token_pool + token_values[param_type_idx];
        }

        // Get param name
        beg int param_name_idx = expect("ID");
        beg char* param_name = token_pool + token_values[param_name_idx];

        // Check for array param part
        beg char* param_array_part = "";
        if peek() == "LSQUARE" {
            next();
            // Get array size
            beg char* size_str = "";
            if peek() == "NUMBER" {
                beg int size_idx = next();
                size_str = token_pool + token_values[size_idx];
            }
            expect("RSQUARE");
            param_array_part = "[" + size_str + "]";
        }

        // Store param
        param_types[n_params] = param_type;
        param_names[n_params] = param_name;
        param_arrays[n_params] = param_array_part;
        n_params = n_params + 1;

        if peek() == "COMMA" {
            next();
        }
    }
    expect("RPAREN");

    // --- Build Parameter List String ---
    beg char* param_list = "";
    beg int i = 0;
    while i < n_params {
        if i > 0 {
            param_list = param_list + ", ";
        }
        param_list = param_list + param_types[i] + " " + param_names[i] + param_arrays[i];
        i = i + 1;
    }

    // --- Check for Prototype (;) or Definition ({) ---
    if peek() == "SEMICOL" {
        // Function Declaration (Prototype)
        next();
        return fn_type + " " + fn_name + "(" + param_list + ");\n";
    }
    else if peek() == "LBRACE" {
        // Function Definition
        next();
        
        // --- Setup local scope ---
        clear_local_symbols();
        i = 0;
        while i < n_params {
            beg char* var_type = param_types[i];
            if param_arrays[i] != "" {
                var_type = var_type + "*"; // Store 'int arr[]' as 'int*'
            }
            add_symbol(0, param_names[i], var_type);
            i = i + 1;
        }
        
        // --- Parse function body ---
        beg char* body = "";
        while peek() != "RBRACE" && peek() != "EOF" {
            body = body + statement() + "\n";
        }
        expect("RBRACE");
        
        // Build final C code
        return fn_type + " " + fn_name + "(" + param_list + ") {\n" + body + "}\n";
    }
    else {
        boo("Error: Expected ';' or '{' after function signature, line " + itos(line_num));
        return "";
    }
}

ah char* statement() {
    // Dispatches to the correct statement parser.
    beg char* tok = peek();
    
    if tok == "LET" {
        return let_stmt(0); // 0 for local
    } else if tok == "PRINT" {
        return print_stmt();
    } else if tok == "IF" {
        return if_stmt();
    } else if tok == "WHILE" {
        return while_stmt();
    } else if tok == "RETURN" {
        return return_stmt();
    } else if tok == "ID" {
        return id_stmt();
    } else if tok == "COMMENT" {
        return comment_stmt();
    } else {
        boo("Error: Unexpected statement: " + tok + " on line " + itos(token_lines[parser_pos]));
        next(); // Consume bad token
        return "";
    }
}

ah char* let_stmt(int is_global) {
    beg int line_num = token_lines[parser_pos];
    expect("LET");

    // --- Get Type ---
    beg char* var_type = "int"; // Default type
    if peek() == "TYPE" {
        beg int var_type_idx = next();
        var_type = token_pool + token_values[var_type_idx];
    }

    // --- Get Name ---
    beg var_name_idx = expect("ID");
    beg char* var_name = token_pool + token_values[var_name_idx];

    // Check redefinition
    if (is_global == 0 && get_symbol_type(0, var_name) != "") ||
       (is_global == 1 && get_symbol_type(1, var_name) != "") {
        
        boo("Error: Redefinition of variable");
        boo(var_name);
        return ""; // Error
    }

    // --- Parsing Cases ---
    if peek() == "ASSIGN" {
        // --- Case 1: Declaration with Assignment (e.g., beg x = 10) ---
        next();
        
        beg char* right_expr = expr();
        beg char* right_type = expr_type;
        
        if var_type == "int" {
            var_type = right_type; // Infer type
        } else if var_type != right_type {
            boo("Error: Incompatible type " + right_type + " to " + var_type + ", line " + itos(line_num));
            return "";
        }
        
        expect("SEMICOL");
        add_symbol(is_global, var_name, var_type);
        
        // C code: e.g., "int x = 5;"
        return var_type + " " + var_name + " = " + right_expr + ";\n";
    }
    else if peek() == "LSQUARE" {
        // --- Case 2: Array Declaration (e.g., beg int arr[10]) ---
        next();

        if var_type == "int" {
            boo("Error: Array declaration must have an explicit type on line" + itos(line_num));
            return "";
        }
        
        beg int size_tok = expect("NUMBER");
        beg char* size = token_pool + token_values[size_tok];
        
        expect("RSQUARE");
        expect("SEMICOL");

        // Store array type as 'base_type*' (e.g., 'int*')
        beg char* array_type = var_type + "*";
        add_symbol(is_global, var_name, array_type);
        
        // C code: e.g., "int arr[10];"
        return var_type + " " + var_name + "[" + size + "];\n";
    }
    else if peek() == "SEMICOL" {
        // --- Case 3: Declaration without Assignment (e.g., beg int x;) ---
        next();
        
        if var_type == "int" {
            boo("Error: Declaration without assignment must have explicit type on line" + itos(line_num));
            return "";
        }
        
        add_symbol(is_global, var_name, var_type);
        
        // C code: e.g., "int x;"
        return var_type + " " + var_name + ";\n";
    }
    else {
        boo("Error: Expected '=', '[', or ';' after variable name on line" + itos(line_num));
        next(); // Consume bad token
        return "";
    }
}

ah char* print_stmt() {
    beg int line_num = token_lines[parser_pos];
    expect("PRINT");
    expect("LPAREN");
    beg char* c_expr = expr();
    beg char* c_type = expr_type;
    expect("RPAREN");
    expect("SEMICOL");
    
    if c_type == "int" {
        return "printf(\"%d\\n\", " + c_expr + ");\n";
    } else if c_type == "char" {
        return "printf(\"%c\\n\", " + c_expr + ");\n";
    } else if c_type == "char*" {
        return "printf(\"%s\\n\", " + c_expr + ");\n";
    } else {
        boo("Error: Unprintable type '" + c_type + "' on line " + itos(line_num));
        return "";
    }
}

ah char* comment_stmt() {
    next();
    return "";
}


// =============================================================
// Expression Parsers
// =============================================================

ah char* expr() {
    // Main entry point for parsing an expression.
    // Returns C code string. Sets global 'expr_type'.
    return logical();
}

ah char* logical() {
    // Handles: expr (&& | ||) expr
    beg char* code = relational();
    beg char* left_type = expr_type;

    while peek() == "OR" || peek() == "AND" {
        beg int op_idx = next();
        beg char* op = op_to_c_op(token_types[op_idx]);

        beg char* right_code = relational();
        beg char* right_type = expr_type;

        // Type check: logical ops must be on ints (or chars)
        if left_type != "int" || right_type != "int" {
            boo("Error: Logical operators '&&' and '||' can only be used on integers, line " + itos(token_lines[op_idx]));
            return "";
        }
        
        code = code + " " + op + " " + right_code;
        expr_type = "int"; // Result is always an int
        left_type = "int";
    }

    expr_type = left_type; // Set final type
    return code;
}

ah char* relational() {
    // Handles: expr (== | != | < | > | <= | >=) expr
    beg char* code = additive();
    beg char* left_type = expr_type;

    while peek() == "EQ" || peek() == "NE" ||
          peek() == "LT" || peek() == "GT" ||
          peek() == "LE" || peek() == "GE" {

        beg int op_idx = next();
        beg char* op = op_to_c_op(token_types[op_idx]);
        beg int line = token_lines[op_idx];

        beg char* right_code = additive();
        beg char* right_type = expr_type;

        // Type check
        if left_type == "char*" && right_type == "char*" {
            if op == "==" {
                code = "strcmp(" + code + ", " + right_code + ") == 0";
            } else if op == "!=" {
                code = "strcmp(" + code + ", " + right_code + ") != 0";
            } else {
                boo("Error: Operator '" + op + "' not allowed on strings, line " + itos(line));
                return "";
            }
        } else if left_type == "char*" || right_type == "char*" {
            boo("Error: Operator '" + op + "' not allowed between string and non-string, line " + itos(line));
            return "";
        } else {
            // int/char comparison
            code = "(" + code + " " + op + " " + right_code + ")";
        }

        expr_type = "int"; // Result is always an int
        left_type = "int";
    }

    expr_type = left_type; // Set final type
    return code;
}

ah char* additive() {
    // Handles: expr (+ | -) expr
    // This also handles pointer arithmetic.
    beg char* code = multiplicative();
    beg char* left_type = expr_type;

    while peek() == "PLUS" || peek() == "MINUS" {
        beg int op_idx = next();
        beg char* op = op_to_c_op(token_types[op_idx]);
        beg int line = token_lines[op_idx];

        beg char* right_code = multiplicative();
        beg char* right_type = expr_type;

        // Case 1: int + int
        if left_type == "int" && right_type == "int" {
            expr_type = "int";
            code = code + " " + op + " " + right_code;
        }
        // Case 2: Pointer Arithmetic
        else if str_ends_with(left_type, '*') && right_type == "int" {
            expr_type = left_type; // e.g., int* + int = int*
            code = code + " " + op + " " + right_code;
        }
        else if left_type == "int" && str_ends_with(right_type, '*') {
            if (op == "+") {
                expr_type = right_type; // int + int* = int*
                code = code + " " + op + " " + right_code;
            } else {
                boo("Error: Cannot subtract a pointer from an integer, line " + itos(line));
                return "";
            }
        }
        // Case 3: String Concat (char* + char*)
        else if left_type == "char*" && right_type == "char*" && op == "+" {
            expr_type = "char*";
            code = "concat(" + code + ", " + right_code + ")";
        }
        // Case 4: Error
        else {
            boo("Error: Operator '" + op + "' not allowed between '" + left_type + "' and '" + right_type + "', line " + itos(line));
            return "";
        }
        
        left_type = expr_type;
    }
    
    expr_type = left_type;
    return code;
}

ah char* multiplicative() {
    // Handles: expr (* | /) expr
    beg char* code = unary();
    beg char* left_type = expr_type;

    while peek() == "MUL" || peek() == "DIV" {
        beg int op_idx = next();
        beg char* op = op_to_c_op(token_types[op_idx]);
        
        beg char* right_code = unary();
        beg char* right_type = expr_type;

        if left_type != "int" || right_type != "int" {
            boo("Error: Operators '*' and '/' can only be used on integers, line " + itos(token_lines[op_idx]));
            return "";
        }
        
        code = code + " " + op + " " + right_code;
        expr_type = "int";
        left_type = "int";
    }
    
    expr_type = left_type;
    return code;
}

ah char* unary() {
    // Handles: -expr
    if peek() == "MINUS" {
        beg int op_idx = next(); // Consume '-'
        
        beg char* code = unary(); // Recursive call
        
        if expr_type != "int" {
            boo("Error: Unary '-' operator can only be applied to integers, line " + itos(token_lines[op_idx]));
            return "";
        }
        
        expr_type = "int";
        return "-" + code;
    }
    
    return atom();
}

ah char* atom() {
    // Handles: literals, variables, (expr), fn_call(), arr[idx]
    // This is the first function to set the global 'expr_type'.

    beg int tok_idx = next();
    beg char* tok_type = token_types[tok_idx];
    beg int tok_val_idx = token_values[tok_idx];
    beg int tok_line = token_lines[tok_idx];
    
    // Case 1: Literals
    if tok_type == "NUMBER" {
        expr_type = "int";
        return token_pool + tok_val_idx;
    }
    else if tok_type == "CHAR" {
        expr_type = "char";
        return token_pool + tok_val_idx;
    }
    else if tok_type == "STRING" {
        expr_type = "char*";
        return token_pool + tok_val_idx;
    }
    
    // Case 2: Parenthesized Expression
    else if tok_type == "LPAREN" {
        beg char* code = expr();
        // expr_type is already set by the call above
        expect("RPAREN");
        return "(" + code + ")";
    }

    // Case 3: Identifier (var, array index, function call)
    else if tok_type == "ID" {
        beg char* var_name = token_pool + tok_val_idx;
        
        // Look for symbol in local, then global scope
        beg char* sym_type = get_symbol_type(0, var_name);
        
        if sym_type == "" {
            boo("Error: Undeclared identifier '" + var_name + "' on line " + itos(tok_line));
            return "";
        }
        
        // Sub-case 3a: Function Call - ID()
        if peek() == "LPAREN" {
            next();
            expr_type = sym_type; // Type is the function's return type
            beg char* c_code = var_name + "(";
            
            beg int arg_count = 0;
            while peek() != "RPAREN" {
                if arg_count > 0 {
                    expect("COMMA");
                    c_code = c_code + ", ";
                }
                c_code = c_code + expr();
                arg_count = arg_count + 1;
            }
            expect("RPAREN");
            return c_code + ")";
        }
        // Sub-case 3b: Array Access - ID[]
        else if peek() == "LSQUARE" {
            if str_ends_with(sym_type, '*') == 0 {
                boo("Error: Variable '" + var_name + "' is not an array and cannot be indexed, line " + itos(tok_line));
                return "";
            }
            next();
            
            beg char* idx_code = expr();
            if expr_type != "int" {
                boo("Error: Array index must be an integer, line " + itos(tok_line));
                return "";
            }
            expect("RSQUARE");
            
            // Set type to the base type (e.g., "int*" -> "int")
            // We need a string function for this.
            // For now, we assume simple types.
            if sym_type == "int*" { expr_type = "int"; }
            else if sym_type == "char*" { expr_type = "char"; }
            else { expr_type = "int"; } // Default assumption
            
            return var_name + "[" + idx_code + "]";
        }
        // Sub-case 3c: Simple Variable
        else {
            expr_type = sym_type;
            return var_name;
        }
    }
    
    // Case 4: Error
    else {
        boo("Error: Unexpected token in expression: " + tok_type + " on line " + itos(tok_line));
        return "";
    }
}


// =============================================================
// Parser Helpers
// =============================================================

ah char* peek() {
    // Returns the type of the current token.
    return token_types[parser_pos];
}

ah int next() {
    // Consumes the current token and returns its index.
    beg int current_pos = parser_pos;
    parser_pos = parser_pos + 1;
    return current_pos;
}

ah int expect(char* kind) {
    // Checks if the current token is of the expected 'kind'.
    // If yes, consumes it and returns its index.
    // If no, prints an error and returns -1.
    
    // Skips comments and gets type
    beg char* tok_type = peek();
    
    if tok_type == kind {
        // Consume and return index
        return next();
    }
    
    // Handle error
    beg int tok_line = token_lines[parser_pos];
    boo("Error: Syntax Error on line " + itos(tok_line));
    boo("Expected token: " + kind);
    boo("... but got token: " + tok_type);
    
    // In a real compiler, we'd exit here.
    return -1; // Indicate error
}


// =============================================================
// Symbol Table Helpers
// =============================================================

ah int clear_local_symbols() {
    // Clears the local (function-level) symbol table.
    // Called when entering a new function.
    n_locals = 0;
    return 0;
}

ah char* get_symbol_type(int is_global, char* name) {
    // Searches for a variable 'name' in the given 'scope'.
    // Returns its type (e.g., "int", "char*") if found.
    // Returns "" (empty string) if not found.
    
    beg int i = 0;
    
    if is_global == 0 {
        while i < n_locals {
            if local_names[i] == name {
                return local_types[i];
            }
            i = i + 1;
        }
    } else { // "global"
        while i < n_globals {
            if global_names[i] == name {
                return global_types[i];
            }
            i = i + 1;
        }
    }
    
    // Not found, check outer scope (if local)
    if is_global == 0 {
        return get_symbol_type(1, name);
    }
    
    return ""; // Not found anywhere
}

ah int add_symbol(int is_global, char* name, char* type) {
    // Adds a new variable to the symbol table.
    // Returns 0 on success.
    // NOTE: This function assumes you have already checked for redefinition.
    if is_global == 0 {
        local_names[n_locals] = name;
        local_types[n_locals] = type;
        n_locals = n_locals + 1;
    } else {
        global_names[n_globals] = name;
        global_types[n_globals] = type;
        n_globals = n_globals + 1;
    }
    return 0;
}


// =============================================================
// Parser Utils
// =============================================================

ah int str_ends_with(char* s, char c) {
    // Checks if string 's' ends with character 'c'.
    // Returns 1 (true) or 0 (false).
    beg int len = strlen(s);
    if len == 0 {
        return 0; // Empty string
    }
    
    if s[len - 1] == c {
        return 1;
    }
    return 0;
}

ah char* op_to_c_op(char* tok_type) {
    // Translates a token type (e.g., "PLUS") to its C operator (e.g., "+").
    if tok_type == "PLUS" { return "+"; }
    if tok_type == "MINUS" { return "-"; }
    if tok_type == "MUL" { return "*"; }
    if tok_type == "DIV" { return "/"; }
    if tok_type == "EQ" { return "=="; }
    if tok_type == "NE" { return "!="; }
    if tok_type == "LT" { return "<"; }
    if tok_type == "GT" { return ">"; }
    if tok_type == "LE" { return "<="; }
    if tok_type == "GE" { return ">="; }
    if tok_type == "AND" { return "&&"; }
    if tok_type == "OR" { return "||"; }
    
    // Should never happen, but good to have a default.
    return ""; 
}


// =============================================================
// Tokenizer
//
// This is the main lexer logic, ported from python/lexer/lexer.py
// =============================================================

ah int tokenize(char* source_code) {
    beg int pos = 0;
    beg int line_num = 1;
    beg int line_start = 0;
    
    beg char buffer[100];
    beg int i = 0;
 
    beg int token_count = 0;
    beg int pool_pos = 0;

    beg char c;
    beg int col;
    beg int j;
    beg char token_val;
    beg int token_start_col;
    
    while source_code[pos] != '\0' {
        c = source_code[pos];
        col = pos - line_start;
        i = 0;

        // --- 1. Skip Whitespace ---
        if is_space(c) {
            if c == '\n' {
                line_num = line_num + 1;
                line_start = pos + 1;
            }
            pos = pos + 1;
        } 
        
        // --- 2. Check for Numbers ---
        else if is_digit(c) {
            token_start_col = col;
            while is_digit(c) {
                buffer[i] = c; i = i + 1; pos = pos + 1; c = source_code[pos];
            }
            if c == '.' {
                buffer[i] = c; i = i + 1; pos = pos + 1; c = source_code[pos];
                while is_digit(c) {
                    buffer[i] = c; i = i + 1; pos = pos + 1; c = source_code[pos];
                }
            }
            buffer[i] = '\0';
            
            token_types[token_count] = "NUMBER";
            token_lines[token_count] = line_num;
            token_cols[token_count] = token_start_col;
            
            // Copy buffer to string pool
            token_values[token_count] = pool_pos;
            j = 0;

            // <= to include the '\0'
            while j <= i {
                token_pool[pool_pos] = buffer[j];
                pool_pos = pool_pos + 1;
                j = j + 1;
            }
            token_count = token_count + 1;
        }
        
        // --- 3. Check for Identifiers & Keywords ---
        else if is_letter(c) {
            token_start_col = col;
            while is_ident_char(c) {
                buffer[i] = c; i = i + 1; pos = pos + 1; c = source_code[pos];
            }
            buffer[i] = '\0';

            beg char* tok_type = check_keywords(buffer);
            token_types[token_count] = tok_type;
            token_lines[token_count] = line_num;
            token_cols[token_count] = token_start_col;

            // Copy buffer to string pool
            if tok_type != "ID" && tok_type != "TYPE" {
                token_values[token_count] = -1;
            } else {
                token_values[token_count] = pool_pos;
                j = 0;
                while j <= i {
                    token_pool[pool_pos] = buffer[j];
                    pool_pos = pool_pos + 1;
                    j = j + 1;
                }
            }
            token_count = token_count + 1;
        }

        // --- 4. Check for Multi-Char Tokens ---
        else if c == '=' {
            if source_code[pos + 1] == '=' {
                add_simple_token(token_count, "EQ", line_num, col);
                token_count = token_count + 1; pos = pos + 2;
            } else {
                add_simple_token(token_count, "ASSIGN", line_num, col);
                token_count = token_count + 1; pos = pos + 1;
            }
        }
        else if c == '!' && source_code[pos + 1] == '=' {
            add_simple_token(token_count, "NE", line_num, col);
            token_count = token_count + 1; pos = pos + 2;
        }
        else if c == '>' {
            if source_code[pos + 1] == '=' {
                add_simple_token(token_count, "GE", line_num, col);
                token_count = token_count + 1; pos = pos + 2;
            } else {
                add_simple_token(token_count, "GT", line_num, col);
                token_count = token_count + 1; pos = pos + 1;
            }
        }
        else if c == '<' {
            if source_code[pos + 1] == '=' {
                add_simple_token(token_count, "LE", line_num, col);
                token_count = token_count + 1; pos = pos + 2;
            } else {
                add_simple_token(token_count, "LT", line_num, col);
                token_count = token_count + 1; pos = pos + 1;
            }
        }
        else if c == '&' && source_code[pos + 1] == '&' {
            add_simple_token(token_count, "AND", line_num, col);
            token_count = token_count + 1; pos = pos + 2;
        }
        else if c == '|' && source_code[pos + 1] == '|' {
            add_simple_token(token_count, "OR", line_num, col);
            token_count = token_count + 1; pos = pos + 2;
        }
        else if c == '/' {
            if source_code[pos + 1] == '/' {
                add_simple_token(token_count, "COMMENT", line_num, col);
                token_count = token_count + 1; pos = pos + 2;
 
                // Loop to skip till after newline or EOL
                while source_code[pos] != '\n' {
                    pos = pos + 1;
                }
            } else {
                add_simple_token(token_count, "DIV", line_num, col);
                token_count = token_count + 1; pos = pos + 1;
            }
        }

        // --- 5. Check for Single-Char Tokens ---
        else if c == '(' {
            add_simple_token(token_count, "LPAREN", line_num, col);
            token_count = token_count + 1; pos = pos + 1;
        }
        else if c == ')' {
            add_simple_token(token_count, "RPAREN", line_num, col);
            token_count = token_count + 1; pos = pos + 1;
        }
        else if c == '{' {
            add_simple_token(token_count, "LBRACE", line_num, col);
            token_count = token_count + 1; pos = pos + 1;
        }
        else if c == '}' {
            add_simple_token(token_count, "RBRACE", line_num, col);
            token_count = token_count + 1; pos = pos + 1;
        }
        else if c == '[' {
            add_simple_token(token_count, "LSQUARE", line_num, col);
            token_count = token_count + 1; pos = pos + 1;
        }
        else if c == ']' {
            add_simple_token(token_count, "RSQUARE", line_num, col);
            token_count = token_count + 1; pos = pos + 1;
        }
        else if c == '+' {
            add_simple_token(token_count, "PLUS", line_num, col);
            token_count = token_count + 1; pos = pos + 1;
        }
        else if c == '-' {
            add_simple_token(token_count, "MINUS", line_num, col);
            token_count = token_count + 1; pos = pos + 1;
        }
        else if c == '*' {
            add_simple_token(token_count, "MUL", line_num, col);
            token_count = token_count + 1; pos = pos + 1;
        }
        else if c == ';' {
            add_simple_token(token_count, "SEMICOL", line_num, col);
            token_count = token_count + 1; pos = pos + 1;
        }
        else if c == ',' {
            add_simple_token(token_count, "COMMA", line_num, col);
            token_count = token_count + 1; pos = pos + 1;
        }

        // --- 6. Handle Strings and Chars ---
        else if c == '"' {
            token_start_col = col;
            pos = pos + 1; c = source_code[pos];

            while c != '"' && c != '\0' {
                if c == '\\' {
                    pos = pos + 1; c = source_code[pos];
                    
                    if c == 'n' { buffer[i] = '\n';
                    } else if c == 't' { buffer[i] = '\t';
                    } else if c == '"' { buffer[i] = '"';
                    } else if c == '\\' { buffer[i] = '\\';
                    } else { buffer[i] = c; }
                }
                else { buffer[i] = c; }
                i = i + 1; pos = pos + 1; c = source_code[pos];
            }
            
            // Check unclosed string
            if c == '\0' { boo("Error: Unclosed string literal!"); return 1; }
            pos = pos + 1;
            buffer[i] = '\0';

            // Add token
            token_types[token_count] = "STRING";
            token_lines[token_count] = line_num;
            token_cols[token_count] = token_start_col;
            token_values[token_count] = pool_pos;
            j = 0;
            while (j <= i) {
                token_pool[pool_pos] = buffer[j];
                pool_pos = pool_pos + 1;
                j = j + 1;
            }
            token_count = token_count + 1;
        }
        else if c == '\'' {
            token_start_col = col;
            pos = pos + 1; c = source_code[pos];
            token_val = c;

            if c == '\\' {
                pos = pos + 1; c = source_code[pos];

                if c == 'n' { token_val = '\n';
                } else if c == 't' { token_val = '\t';
                } else if c == '\'' { token_val = '\'';
                } else if c == '\\' { token_val = '\\';
                } else { token_val = c; }
            }

            pos = pos + 1; c = source_code[pos];
            if c != '\'' { boo("Error: Unclosed or invalid char literal!"); return 1; }
            pos = pos + 1;
            
            // Add token
            buffer[0] = token_val; buffer[1] = '\0';
            token_types[token_count] = "CHAR";
            token_lines[token_count] = line_num;
            token_cols[token_count] = token_start_col;
            token_values[token_count] = pool_pos;
            token_pool[pool_pos] = buffer[0];
            token_pool[pool_pos + 1] = buffer[1];
            pool_pos = pool_pos + 2;
            token_count = token_count + 1;
        }

        // --- 7. Handle Errors ---
        else {
            boo("Error: Unexpected character!");
            boo(ctos(c));
            return 1;
        }
    }
    
    // Add EOF Token
    add_simple_token(token_count, "EOF", line_num, col);
    token_count = token_count + 1;

    return token_count;
}

// =============================================================
// Lexer Helpers
//
// We port the logic from the Python lexer.
// =============================================================

ah int is_letter(char c) {
    // Checks if a character is a letter or underscore.
    // Corresponds to: [A-Za-z_]
    return (c >= 'a' && c <= 'z') || 
           (c >= 'A' && c <= 'Z') || 
           (c == '_');
}

ah int is_digit(char c) {
    // Checks if a character is a 0-9 digit.
    // Corresponds to: \d
    return (c >= '0' && c <= '9');
}

ah int is_space(char c) {
    // Checks for whitespace characters to skip.
    // Corresponds to: [ \t\n]
    return (c == ' ') || (c == '\t') || (c == '\n');
}

ah int is_ident_char(char c) {
    // Checks if a char can be part of an identifier *after* the first char.
    // Corresponds to: [A-Za-z0-9_]
    return is_letter(c) || is_digit(c);
}

ah char* check_keywords(char* s) {
    // Checks if a string 's' is a keyword.
    // If it is, return the keyword's Token Type.
    // Otherwise, return "ID".
    if s == "ah" {
        return "FN";
    } else if s == "beg" {
        return "LET";
    } else if s == "boo" {
        return "PRINT";
    } else if s == "if" {
        return "IF";
    } else if s == "else" {
        return "ELSE";
    } else if s == "while" {
        return "WHILE";
    } else if s == "return" {
        return "RETURN";
    } else if s == "int*" || s == "char*" ||
              s == "int" || s == "char" ||
              s == "void"  {
        return "TYPE";
    }

    // Default case: not a keyword
    return "ID";
}

// Helper to add a simple token (without a value) to the token arrays.
ah int add_simple_token(int index, char* type, int line, int col) {
    token_types[index] = type;
    token_values[index] = -1; // -1 means no value
    token_lines[index] = line;
    token_cols[index] = col;
    return 0;
}

